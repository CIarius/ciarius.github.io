       IDENTIFICATION DIVISION.
       PROGRAM-ID. EMPLOYEES.
      * load employee data from CSV file into an indexed file
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
         SELECT EMP-IN-FILE
           ASSIGN TO "employees.csv"
           ORGANIZATION IS LINE SEQUENTIAL
           FILE STATUS IS WS-FS-IN.
         SELECT EMP-OUT-FILE
           ASSIGN TO "employees.idx"
           ORGANIZATION IS INDEXED
           ACCESS MODE IS DYNAMIC
           RECORD KEY IS EMP-ID
           FILE STATUS IS WS-FS-OUT.

       DATA DIVISION.
       FILE SECTION.
       FD EMP-IN-FILE
          RECORD CONTAINS 512 CHARACTERS
          LABEL RECORDS ARE STANDARD.
       01 IN-LINE PIC X(512).

       FD  EMP-OUT-FILE.
       COPY "EMPLOYEES.CPY".

       WORKING-STORAGE SECTION.

       01 WS-FS-IN  PIC XX.
       01 WS-FS-OUT PIC XX.

       01 WS-EMP-ID-TXT     PIC X(20).
       01 WS-FIRST-TXT      PIC X(40).
       01 WS-LAST-TXT       PIC X(40).
       01 WS-GENDERID-TXT   PIC X(5).
       01 WS-DOB-TXT        PIC X(20).
       01 WS-AGE-TXT        PIC X(5).
       01 WS-DEPTID-TXT     PIC X(5).
       01 WS-ENTRY-TXT      PIC X(20).
       01 WS-LOS-TXT        PIC X(20).
       01 WS-ROLEID-TXT     PIC X(5).
       01 WS-DUMMY-TXT      PIC X(5).

       01 WS-DOB-YYYY-TXT   PIC X(4).
       01 WS-DOB-MM-TXT     PIC X(2).
       01 WS-DOB-DD-TXT     PIC X(2).

       01 WS-ENTRY-YYYY-TXT PIC X(4).
       01 WS-ENTRY-MM-TXT   PIC X(2).
       01 WS-ENTRY-DD-TXT   PIC X(2).

       01 CR  PIC X VALUE X"0D".
       01 LF  PIC X VALUE X"0A".
       01 TAB PIC X VALUE X"09".

       PROCEDURE DIVISION.

       MAIN-LOGIC.

           OPEN INPUT EMP-IN-FILE
                OUTPUT EMP-OUT-FILE.

           IF WS-FS-IN NOT = "00"
               DISPLAY "OPEN INPUT FAILED, FS=" WS-FS-IN
               STOP RUN
           END-IF.

           IF WS-FS-OUT NOT = "00"
               DISPLAY "OPEN OUTPUT FAILED, FS=" WS-FS-OUT
               STOP RUN
           END-IF.

           PERFORM LOAD-LOOP UNTIL WS-FS-IN = "10".

           CLOSE EMP-IN-FILE
                 EMP-OUT-FILE.
           DISPLAY "LOAD COMPLETE.".
           STOP RUN.

       LOAD-LOOP.

           READ EMP-IN-FILE
               AT END MOVE "10" TO WS-FS-IN
               NOT AT END
                   PERFORM PROCESS-LINE
           END-READ.

       PROCESS-LINE.

           DISPLAY IN-LINE.

           UNSTRING IN-LINE DELIMITED BY ","
             INTO WS-EMP-ID-TXT
                  WS-FIRST-TXT
                  WS-LAST-TXT
                  WS-GENDERID-TXT
                  WS-DOB-TXT
                  WS-AGE-TXT
                  WS-DEPTID-TXT
                  WS-ENTRY-TXT
                  WS-LOS-TXT
                  WS-ROLEID-TXT
                  WS-DUMMY-TXT
           END-UNSTRING.

           INSPECT WS-FIRST-TXT  REPLACING ALL '"' BY SPACE.
           INSPECT WS-LAST-TXT   REPLACING ALL '"' BY SPACE.
           INSPECT WS-DOB-TXT    REPLACING ALL '"' BY SPACE.
           INSPECT WS-ENTRY-TXT  REPLACING ALL '"' BY SPACE.
           INSPECT WS-LOS-TXT    REPLACING ALL '"' BY SPACE.

           INSPECT WS-EMP-ID-TXT   REPLACING ALL X"0D" BY SPACE.
           INSPECT WS-EMP-ID-TXT   REPLACING ALL X"0A" BY SPACE.
           INSPECT WS-EMP-ID-TXT   REPLACING ALL X"09" BY SPACE.

           INSPECT WS-GENDERID-TXT REPLACING ALL X"0D" BY SPACE.
           INSPECT WS-GENDERID-TXT REPLACING ALL X"0A" BY SPACE.
           INSPECT WS-GENDERID-TXT REPLACING ALL X"09" BY SPACE.

           INSPECT WS-AGE-TXT      REPLACING ALL X"0D" BY SPACE.
           INSPECT WS-AGE-TXT      REPLACING ALL X"0A" BY SPACE.
           INSPECT WS-AGE-TXT      REPLACING ALL X"09" BY SPACE.

           INSPECT WS-DEPTID-TXT   REPLACING ALL X"0D" BY SPACE.
           INSPECT WS-DEPTID-TXT   REPLACING ALL X"0A" BY SPACE.
           INSPECT WS-DEPTID-TXT   REPLACING ALL X"09" BY SPACE.

           INSPECT WS-ROLEID-TXT   REPLACING ALL X"0D" BY SPACE.
           INSPECT WS-ROLEID-TXT   REPLACING ALL X"0A" BY SPACE.
           INSPECT WS-ROLEID-TXT   REPLACING ALL X"09" BY SPACE.

           INSPECT WS-LOS-TXT      REPLACING ALL X"0D" BY SPACE.
           INSPECT WS-LOS-TXT      REPLACING ALL X"0A" BY SPACE.
           INSPECT WS-LOS-TXT      REPLACING ALL X"09" BY SPACE.

           UNSTRING WS-DOB-TXT DELIMITED BY "/"
             INTO WS-DOB-DD-TXT WS-DOB-MM-TXT WS-DOB-YYYY-TXT
           END-UNSTRING.

           UNSTRING WS-ENTRY-TXT DELIMITED BY "/"
             INTO WS-ENTRY-DD-TXT WS-ENTRY-MM-TXT WS-ENTRY-YYYY-TXT
           END-UNSTRING.

           IF WS-EMP-ID-TXT     = SPACES MOVE "0" TO WS-EMP-ID-TXT.
           IF WS-GENDERID-TXT   = SPACES MOVE "0" TO WS-GENDERID-TXT.
           IF WS-DEPTID-TXT     = SPACES MOVE "0" TO WS-DEPTID-TXT.
           IF WS-ROLEID-TXT     = SPACES MOVE "0" TO WS-ROLEID-TXT.
           IF WS-DOB-YYYY-TXT   = SPACES MOVE "0" TO WS-DOB-YYYY-TXT.
           IF WS-DOB-MM-TXT     = SPACES MOVE "0" TO WS-DOB-MM-TXT.
           IF WS-DOB-DD-TXT     = SPACES MOVE "0" TO WS-DOB-DD-TXT.
           IF WS-ENTRY-YYYY-TXT = SPACES MOVE "0" TO WS-ENTRY-YYYY-TXT.
           IF WS-ENTRY-MM-TXT   = SPACES MOVE "0" TO WS-ENTRY-MM-TXT.
           IF WS-ENTRY-DD-TXT   = SPACES MOVE "0" TO WS-ENTRY-DD-TXT.

           MOVE WS-FIRST-TXT TO EMP-FIRST-NAME.

           MOVE WS-LAST-TXT TO EMP-LAST-NAME.

           MOVE FUNCTION NUMVAL-C(WS-EMP-ID-TXT)       TO EMP-ID.
           MOVE FUNCTION NUMVAL-C(WS-GENDERID-TXT)     TO EMP-GENDER-ID.

           MOVE FUNCTION NUMVAL-C(WS-DOB-YYYY-TXT)     TO EMP-DOB-YYYY.
           MOVE FUNCTION NUMVAL-C(WS-DOB-MM-TXT)       TO EMP-DOB-MM.
           MOVE FUNCTION NUMVAL-C(WS-DOB-DD-TXT)       TO EMP-DOB-DD.

           MOVE FUNCTION NUMVAL-C(WS-DEPTID-TXT)       TO EMP-DEPT-ID.
           MOVE FUNCTION NUMVAL-C(WS-ROLEID-TXT)       TO EMP-ROLE-ID.

           MOVE FUNCTION NUMVAL-C(WS-ENTRY-YYYY-TXT)   TO EMP-ENTRY-YYYY.
           MOVE FUNCTION NUMVAL-C(WS-ENTRY-MM-TXT)     TO EMP-ENTRY-MM.
           MOVE FUNCTION NUMVAL-C(WS-ENTRY-DD-TXT)     TO EMP-ENTRY-DD.

           WRITE EMP-REC
             INVALID KEY
               DISPLAY "DUPLICATE KEY FOR EMP-ID=" EMP-ID.
